//------------------------------------------------REGOLE SINTATTICHE PER LA GRAMMATICA DELLE REGOLE SINTATTICHE

/*Una grammatica è un insieme di regole*/

	<grammatica> ::= <regole>;

/*Una insieme di regole può essere una singola regola
 *oppure una regola seguita da un insieme di regole.*/
 
	<regole> ::= <regola> | <regola> <regole>;

/*Una regola può cominciare con l'indicazione di un determinato
 *numero di simboli di lookahead necessari al parser generato
 dal compilatore "JavaCC".
 La parte sinistra della regola (testa) è un simbolo non terminale
 della grammatica. A seguire vi è il simbolo "può essere" che rappresenta
 il confine tra la testa ed il corpo della regola di produzione.
 Successivamente vi è il "corpo" (parte destra della regola di produzione).
 In fine il simbolo "punto e virgola" rappresenta la terminazione della regola.*/
	
	<regola> ::= [OP_LOOKAHEAD PARAPERTA_T NUM_LOOKAHEAD PARCHIUSA_T] NON_TERM PUO_ESSERE <corpo> PV;

/*Il corpo di una regola di produzione può essere un insieme di elementi
 *oppure un insieme di elementi seguiti dal simbolo "pipe" e da un altro corpo.*/
 
	<corpo> ::= <elementi> | <elementi> PIPE <corpo>;

/*Un insieme di elementi di una regola di produzione possono essere un singolo
 *elemento oppure un elemento concatenato ad un insieme di elementi.*/
	
	<elementi> ::= <elemento> | <elemento> <elementi>;

/*Un elemento di una regola di produzione può essere un simbolo terminale o
  un simbolo non terminale, oppure un insieme di elementi opzionali, o "epsilon"*/
	
	<elemento> ::= TERM | NON_TERM | eps | PARAPERTA_Q <elementi> PARCHIUSA_Q ;
	

//-----------------------------------------------------------------------------------------------FATTORIZZAZIONI

/*-------------------------------------------------------------------------1) 
 *Bisogna fattorizzare la regola di produzione seguente:*/

	<regole> ::= <regola> | <regola> <regole>;

/*Mettiamo a fattor comune il non terminale "<regola>", ottenendo:*/

	<regole> ::= <regola> <regola_queue>;
	<regola_queue> ::= <regole> | eps;
	
/*-------------------------------------------------------------------------2)
 *Bisogna fattorizzare la regola di produzione seguente:*/

	<corpo> ::= <elementi> | <elementi> PIPE <corpo>;

/*Mettiamo a fattor comune il non terminale "<elementi>", ottenendo:*/

	<corpo> ::= <elementi> <elementi_queue>;
	<elementi_queue> ::= PIPE <corpo> | eps;

/*-------------------------------------------------------------------------3)
 *Bisogna fattorizzare la regola di produzione seguente:*/

	<elementi> ::= <elemento> | <elemento> <elementi>;

/*Mettiamo a fattor comune il non terminale "<elemento>", della derivazione di "<elementi>", ottenendo:*/

	<elementi> ::= <elemento> <elemento_queue>;
	<elemento_queue> ::= <elementi> | eps;
	
/*------------------------------------------------------------------RISULTATO

	<grammatica> ::= <regole>;
	
	<regole> ::= <regola> <regola_queue>;
	<regola_queue> ::= <regole> | eps;
	
	<regola> ::= [OP_LOOKAHEAD PARAPERTA_T NUM_LOOKAHEAD PARCHIUSA_T] NON_TERM PUO_ESSERE <corpo> PV;
	
	<corpo> ::= <elementi> <elementi_queue>;
	<elementi_queue> ::= PIPE <corpo> | eps;
	
	<elementi> ::= <elemento> <elemento_queue>;
	<elemento_queue> ::= <elementi> | eps;
	
	<elemento> ::= TERM | NON_TERM | eps | PARAPERTA_Q <elementi> PARCHIUSA_Q;
 